PROCEDURE      INC_BODY_DBA.RUN_MEMBER:T  (157542,59818)  |Last amended Feb 13, 2014,16:36:58
program  crwarn
CALL WPF.SYSTEM.DEFINE_VARS
CALL WPF.SYSTEM.INI_VARS

write(cgi) '<h3>Run Member'
if('<GSUBMIT>' eq 'Run Member') write(cgi) [datec(today(0), 'mm/dd/yyyy')] ' by ' [sglobal('USER_NAME')]
write(cgi) '</h3>'

write(cgi) '<form name="pickmem" action="<mycgi>" method="post" >'
write(cgi) '<input type="hidden" id="member" name="member" value="WPF.INC_DBA.RUN_MEMBER">'
CALL WPF.INC_ELEMENTS.CASE_IS
write(cgi) '  <table width="100%"><tr>'
write(cgi) '  <th>Family</th><th>Member</th></tr>'
write(cgi) '  <td><select name="runfamnum" class="widthwide" onchange="selFamily(this.value);">'
for EACH_FAMILY =  1,  system(57)
. STR$1 = '    <option value  = "'  + format(EACH_FAMILY) +  '">' +  famname(EACH_FAMILY) + '</option>'
. write(cgi) STR$1
end for
write(cgi) '  </select></td>'

c add first family members
FAMILY_NAME = famname(1)
write(cgi) '  <td><select name="runmemname" id="runmemname" class="widthwide">'
for EACH_MEMBER = 1, MEMCOUNT(FAMILY_NAME)
. MEMBER_NAME = memname(FAMILY_NAME, EACH_MEMBER)
. write(cgi) '    <option>'MEMBER_NAME '</option>'
end for
write(cgi) '  </select></td></tr>'
write(cgi) '</table>'
write(cgi) '<p><input type="submit" name="submit" value="Run Member" class="widthmed">'
write(cgi) '<input type="submit" name="submit" value="View Member"class="widthmed"></p>'
write(cgi) '</form>'

CIF EQ '<GSUBMIT>', 'Run Member'
. write '<h2 class="smallfont"><p><GRUNFAMNAME>.<GRUNMEMNAME><br>Results</p></h2>'
. ifthen(meminfo('<GRUNFAMNAME>.<GRUNMEMNAME>', 2) eq 0)
.   EXECUTE DBMS  'CALL <GRUNFAMNAME>.<GRUNMEMNAME>'
. else
.   write(cgi) 'Sorry this is password protected' [MEMINFO('<GRUNFAMNAME>.<GRUNMEMNAME>', 2)]
. endif
. write(cgi) '<h2 class="smallfont"><p>End Results</p></h2>'
CIF END

CIF EQ '<GSUBMIT>', 'View Member'
. IOSTAT__4 = 0
. ANY_NUM__4 = 0
. write '<h4><GRUNFAMNAME>.<GRUNMEMNAME></h4>'
. write(cgi) '<textarea cols="90" rows="500" name="membertext" READONLY>'
. while (IOSTAT__4  = 0)
.   read(<GRUNFAMNAME>.<GRUNMEMNAME>, iostat = IOSTAT__4 member) STR$1 (A600)
.   write(cgi) STR$1
.   ANY_NUM__4 = ANY_NUM__4 + 1
.   if(ANY_NUM__4 ge 499) IOSTAT__4 = 9
. end while
. if(ANY_NUM__4 GE 499) write(cgi) 'ONLY THE FIRST 500 LINES OF THE MEMBER ARE DISPLAYED'
. write(cgi) '</textarea>'
CIF END

end program
END PROCEDURE
