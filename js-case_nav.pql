PROCEDURE      JS.CASE_NAV:T  (157542,65223)  |Last amended Feb 13, 2014,18:07:03
c Browse case ids

CIF EQ <GCASED>, 1
retrieval crwarn noautocase
string  *   600   STR$1                      | General String use at any time
real    *     8   ANY_NUM__4                  | Use any time you want
string  *    32   VNAME$                     | Variable name
integer *     1   IOSTAT__4                  | Return code of IOSTAT
string  *    32   FIRST_CASE__4               | 1st case
string  *    32   LAST_CASE__4                | last case
string  *    32   CASE_STR                   | Value of CASE

c options can not have onclicks.  Must define function and then CALL it depending on what is passed
open casenav /write lrecl = 400 IOSTAT =  IOSTAT__4

. ifthen (system(39) eq 0)
.   write 'oppss no database is attached can not created case navigation javascript file'
.   exit program  | Exit if no database is attached at this point
. endif

ifNOTthen (IOSTAT__4  = 0)
. write 'Error creating casenav files: ' IOSTAT__4
. exit program
endif

VNAME$ = trimlr(keyname(0, 1))
process cases count = 1 lock = 2
. FIRST_CASE__4 =  trimr(varget(VNAME$))
end cases
process cases reverse count = 1 lock = 2
. LAST_CASE__4  =  trimr(varget(VNAME$))
end cases
write(casenav) 'var caseArray = new Array(), '
write(casenav) '    caseNum = 0;'
ANY_NUM__4  = 1
process cases count = 49 lock = 2
. CASE_STR = trimr(varget(VNAME$))
. STR$1  = 'caseArray[' +  format(ANY_NUM__4)  + ']="'  + CASE_STR  + '";'
. ANY_NUM__4 = ANY_NUM__4 + 1
. write(casenav) STR$1
end cases
ANY_NUM__4 = 100
process cases reverse count = 49 lock = 2
. CASE_STR = trimr(varget(VNAME$))
. STR$1 ='caseArray[' +  format(ANY_NUM__4)  + ']="'  + CASE_STR  + '";'
. ANY_NUM__4 = ANY_NUM__4 - 1
. write(casenav) STR$1
end cases
write(casenav) 'function forwardCase (someNum) {'
write(casenav) '    caseNum = someNum + 1;'
write(casenav) '    if(caseArray[caseNum] == null) {'
write(casenav) '        alert("Please use the list of IDs.");'
write(casenav) '        caseNum = caseNum - 1;'
write(casenav) '    }'
write(casenav) '    else {'
write(casenav) '        document.getElementById("caseis").value = caseArray[caseNum];'
write(casenav) '    }'
write(casenav) '}'
write(casenav) 'function backwardCase (someNum) {'
write(casenav) '    caseNum = someNum - 1;'
write(casenav) '    if(caseArray[caseNum] == null) {'
write(casenav) '        alert("Please use the list of IDs.");'
write(casenav) '        caseNum = caseNum + 1;'
write(casenav) '        }'
write(casenav) '    else {'
write(casenav) '        document.getElementById("caseis").value = caseArray[caseNum];'
write(casenav) '    }'
write(casenav) '}'

close casenav
end retrieval
CIF END
END PROCEDURE
