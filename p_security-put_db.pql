PROCEDURE      P_SECURITY.PUT_DB:T  (157452,47338)  |Last amended Nov 15, 2013,13:08:58
cif eq '<GDBPICK>', '-1'
. global GSOMEMESS = 'You must pick a database'
call WPF.INC_BODY_FORMS.DB_CHOOSE
CALL WPF.CONNECTIONS.FULL_EXIT

cif end


retrieval UPDATE crwarn
string  *    32   DBNAME__4                  | Database Name
real    *     8   ANYNUM__4                  | Use any time you want
integer *     1   GOON__4                    | Control flow variable
SET GOON__4 (0)
DBNAME__4 = sglobal('GDBPICK')

. old rec is DBADMIN via (DBNAME__4)
.   GET VARS FOLDER SESSIONDB
.   ANYNUM__4 = globals('GDB' , trimlr(SESSIONDB))
.   ANYNUM__4 = globals('GDBPATH', trimlr(FOLDER))
.   ANYNUM__4 = globals('GMSTIP', MASTER_IP)
.   ANYNUM__4 = globals('GMSTPORT', MASTER_PORT)
.   GOON__4 = 1
. end rec

c double checks, DB path is correct and user has access to database
. ifthen (GOON__4 eq 0)
.   ANYNUM__4 = globals('GSOMEMESS', 'Database name:' + DBNAME__4 + ' was not found')
.   ANYNUM__4 = globals('GMEMIN', 'WPF.INC_BODY_FORMS.DB_CHOOSE')
.   EXIT RETRIEVAL
. else
    GOON__4 = fileis((FOLDER + SESSIONDB + '.sr1'))
.   ifnotthen(GOON__4 eq 1)
.     ANYNUM__4 = globals('GSOMEMESS', 'Sorry,  ' + DBNAME__4 + ' is not available right now')
.     ANYNUM__4 = globals('GMEMIN', 'WPF.INC_BODY_FORMS.DB_CHOOSE')
.     EXIT RETRIEVAL
.   endif
. endif

. ifthen ('<USERID>' NE '')
.   ifthen ('<SESSIONID>' NE '')
.     record is SESSIONS via ('<USERID>' '<SESSIONID>')
.       LASTREQUESTDATE = today(0)
.       LASTREQUESTTIME = now(0)
.       SESSIONDB = DBNAME__4
.     end rec is
.   ELSE
.     write 'BLANK SESSION ID'
.   endif
. ELSE
.   write 'BLANK USERID'
. endif

end retrieval
END PROCEDURE
