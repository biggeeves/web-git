PROCEDURE      P_SECURITY.PUT_DB:T  (157542,79895)  |Last amended Feb 13, 2014,22:11:35
cif eq '<GDB_PICK>', '-1'
. global GSOMEMESS = 'You must pick a database'
call WPF.INC_BODY_FORMS.DB_CHOOSE
CALL WPF.CONNECTIONS.FULL_EXIT

cif end


retrieval UPDATE crwarn
string  *    32   DB_NAME__4                  | Database Name
real    *     8   ANY_NUM__4                  | Use any time you want
integer *     1   GOON__4                    | Control flow variable
SET GOON__4 (0)
DB_NAME__4 = sglobal('GDB_PICK')
. old rec is DB_INFO via (DB_NAME__4)
.   GET VARS FOLDER WEB_DB_NAME
.   ANY_NUM__4 = globals('GDB' , trimlr(WEB_DB_NAME))
.   ANY_NUM__4 = globals('GDB_PATH', trimlr(FOLDER))
.   ANY_NUM__4 = globals('GMST_IP', MASTER_IP)
.   ANY_NUM__4 = globals('GMST_PORT', MASTER_PORT)
.   GOON__4 = 1
. end rec

c double checks, DB path is correct and user has access to database
. ifthen (GOON__4 eq 0)
.   ANY_NUM__4 = globals('GSOMEMESS', 'Database name:' + DB_NAME__4 + ' was not found')
.   ANY_NUM__4 = globals('GMEMIN', 'WPF.INC_BODY_FORMS.DB_CHOOSE')
.   EXIT RETRIEVAL
. else
    GOON__4 = fileis((FOLDER + WEB_DB_NAME + '.sr1'))
.   ifnotthen(GOON__4 eq 1)
.     ANY_NUM__4 = globals('GSOMEMESS', 'Sorry,  ' + DB_NAME__4 + ' is not available right now')
.     ANY_NUM__4 = globals('GMEMIN', 'WPF.INC_BODY_FORMS.DB_CHOOSE')
.     EXIT RETRIEVAL
.   endif
. endif

. ifthen ('<USER_ID>' NE '')
.   ifthen ('<SESSION_ID>' NE '')
.     record is SESSIONS via ('<USER_ID>' '<SESSION_ID>')
.       LAST_REQUEST_DATE = today(0)
.       LAST_REQUEST_TIME = now(0)
.       WEB_DB_NAME = DB_NAME__4
.     end rec is
.   ELSE
.     write 'BLANK SESSION ID'
.   endif
. ELSE
.   write 'BLANK USER_ID'
. endif

end retrieval
END PROCEDURE
