PROCEDURE      INSTALLER.MAKE_APACHE_CONFIG:T  (157540,35704)  |Last amended Feb 11, 2014,09:55:04
program
. string * 4000 STR$1
. string *  200 MODIFY_TEXT

. STRING * 100 MEMBER_PATH
. STRING * 200 APACHE_CONFIG_PATH
. integer * 2 MY_IOSTAT
. INTEGER * 2 RESVAR
. MEMBER_PATH = 'INSTALL_SRP.INSTALLER.APACHE_CONFIG'
. APACHE_CONFIG_PATH = dsn('APACHE_CONFIG')

. open TMP_MEM DSNVAR = MEMBER_PATH read iostat = MY_IOSTAT lrecl= 600 member
. IFNOTTHEN (MY_IOSTAT = 0)
.   DISPLAY INFOBOX 'COULD NOT READ INSTALL.APACHE_CONFIG. SKIPPING APACH CONFIG: ERROR-' + FORMAT(MY_IOSTAT)
.   EXIT PROGRAM
. ENDIF

. IFTHEN (FILEIS (DSN('APACHE_CONFIG')) = 1)
.   DISPLAY YESNOBOX 'OVERWRITE APACHE CONFIG FILE?  (there is no way to get it back if you do.)' RESPONSE RESVAR
.   IF (RESVAR EQ 0 ) display infobox 'OK. Exiting make apache config'; EXIT PROGRAM
. ENDIF

. open APCON DSNVAR = APACHE_CONFIG_PATH WRITE iostat = my_iostat lrecl= 600
. ifNOTthen (MY_IOSTAT EQ 0)
.   display infobox 'Failed to Open Apache Config File'
.   exit program
. ENDIF
. write (APCON) '# CREATED BY THE WEB INSTALLER ON ' [DATEC(TODAY(0),'Mmm. DD, YYYY')] /
. WRITE (APCON) '# CUSTOMIZE THIS FILE ANY WAY YOU.  Make sure you back it up!'

. loop
.   read (TMP_MEM, iostat = MY_IOSTAT) STR$1 (A200)
.   IFNOT (MY_IOSTAT EQ 0) EXIT LOOP
.   IFTHEN (PATTERN ( STR$1, '@WP_SERVERROOT@') EQ 1)
.     MODIFY_TEXT = DSN('APACHE')
.     EXECUTE SUBPROCEDURE MODIFIER
.     STR$1 =  replace(STR$1, 'WP_SERVERROOT', MODIFY_TEXT , 2, 1, 0)
.   ELSEIF (PATTERN ( STR$1, '@WP_DOCUMENTROOT@') EQ 1)
.     MODIFY_TEXT = DSN('WEB_ROOT')
.     EXECUTE SUBPROCEDURE MODIFIER
.     STR$1 =  replace(STR$1, 'WP_DOCUMENTROOT',REPLACE( MODIFY_TEXT, '\', '/', 9,0,0)  , 2 , 1, 0 )
.   ELSEIF (PATTERN ( STR$1, '@WP_SCRIPTALIAS@') EQ 1)
.     MODIFY_TEXT = DSN('SCRIPTALIAS')
.     EXECUTE SUBPROCEDURE MODIFIER
.     STR$1 =  replace(STR$1, 'WP_SCRIPTALIAS', REPLACE(MODIFY_TEXT, '\', '/', 9,0,0) ,2 , 1, 0 )
.   ENDIF
.   WRITE (APCON) STR$1
. END LOOP

. CLOSE APCON

. close TMP_MEM

SUBPROCEDURE MODIFIER
. IF(EXISTS(MODIFY_TEXT) EQ 0) DISPLAY INFOBOX 'DSN VALUE MISSING. ERRORS IN CONFIG FILE! '
. MODIFY_TEXT = REPLACE(MODIFY_TEXT, '\', '/', 9,0,0)
. IFTHEN (SUBSTR(MODIFY_TEXT, LEN(MODIFY_TEXT), 1) = '/' )
.   MODIFY_TEXT = SUBSTR( MODIFY_TEXT, 1, LEN(MODIFY_TEXT)-1 )
. ENDIF
. IF (EXISTS(MODIFY_TEXT) EQ 0) MODIFY_TEXT = 'DSN ERROR IN INSTALLATION. DSN ERROR IN INSTALLATION.'
WRITE MODIFY_TEXT
END SUBPROCEDURE

end program
END PROCEDURE
