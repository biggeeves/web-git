PROCEDURE      INC_DBA.DAYS_ENTRY:T  (157548,45610)  |Last amended Feb 19, 2014,12:40:10
PROGRAM
. date              VARDATE__4 ('MMIDDIYYYY')  | Validating Date values
. ifthen(nglobal('GDAY') eq -1)
.   VARDATE__4 = today(0)
. else
.   VALID_RESULT = validate(-1, 'VARDATE__4', '<GDAY>')
.   ifthen (VALID_RESULT eq 0)
.     VARDATE__4 = sglobal('GDAY')
.     ifthen (exists(cdate(vardate__4, 'mmiddiyyyy')) eq 0)
.       write(cgi)'<h3 class="smallfont">Please pick a valid date. <GDAY> is invalid </h3>'
.       VARDATE__4 = today(0)
.       ANY_NUM__4 = globaln('GDAY', -1)
.     else
.       write(cgi)'<h3 class="smallfont">Data Entry for: ' [datec(VARDATE__4, 'Wwwwwwwww MM.DD.YYYY')] '</h3>'
.     endif
.   else
.     write(cgi)'<h3 class="smallfont">Please pick a valid date.</h3>'
.     VARDATE__4 = today(0)
.     ANY_NUM__4 = globaln('GDAY', -1)
.   endif
. endif
. write(cgi) '<form action="<mycgi>" method="post">'
. write(cgi) '  <p>Choose a day:  <input type="hidden" name="member" id="member" value="WPF.INC_DBA.DAYS_ENTRY">'
. write(cgi) '  <input type="text" name="gday" id="gday" value="' [datec(VARDATE__4, 'MM.DD.YYYY')] '">'
. write(cgi) '  <input type="submit" name="submit" id="submit">'
. write(cgi) ' </p>'
. write(cgi) '</form>'
END PROGRAM

GLOBAL GTOTAL_CNT = 0

CIF ne '<GDAY>', '-1'
do repeat RECN =  1 <GREC1>
                 <GREC2>
                 <GREC3>
                 <GREC4>
                 <GREC5>


retrieval CRWARN
CALL WPF.SYSTEM.DEFINE_VARS
CALL WPF.SYSTEM.INI_VARS
set COUNTS__4 (0)
VARDATE__4 = '<GDAY>'
REC_TYPE_NUMBER = RECN
ifthen (len(recdoc(REC_TYPE_NUMBER, 0)) gt 0)
. REC_LABEL__4 = pack(recdoc(REC_TYPE_NUMBER, 0))
elseif (len(recdoc(REC_TYPE_NUMBER, 1)) gt 0)
. REC_LABEL__4 = pack(recdoc(REC_TYPE_NUMBER, 1))
else
. REC_LABEL__4 = pack(recname(REC_TYPE_NUMBER))
endif
write(cgi) '<h3>' REC_LABEL__4  | leave the H3 open and close it once record is found.

CIF EQ 1, 0<GCASED>
process cases
CIF END

. process rec RECN
.   ifnot (nget('DCCDATE') eq VARDATE__4) next rec
.   COUNTS__4 = COUNTS__4 + 1
.   ANY_NUM__4 = globaln('GTOTAL_CNT', nglobal('GTOTAL_CNT')  + 1)
.   if(COUNTS__4 eq 1 ) write (cgi) '</h3><p><ul>'
.   execute subprocedure START_ROW
. end rec

CIF EQ 1, 0<GCASED>
end cases
CIF END

ifthen (COUNTS__4 eq 0)
. write(cgi) ': None</h3>'
else
. write (cgi) '</ul></p>'
. write(cgi) '<p class="leftindent">Total of ' COUNTS__4 ' Records found</p>'
endif

subprocedure START_ROW
.   STR$2 = ''
.   SHOW_KEYS__4 = ''
.   ifthen (dbtype(0) eq 1)
.     KEYVNAME__4 = pack(keyname(REC_TYPE_NUMBER, 1))
.     CASE_STR = trimlr(varget(KEYVNAME__4))
.     SHOW_KEYS__4 = '<li> ' + SHOW_KEYS__4 + KEYVNAME__4 +  ':<b> ' + CASE_STR  + '</b>  '
.   endif

.   for EACH_KEY = 1 + dbtype(0), nkeys(REC_TYPE_NUMBER) +  dbtype(0)
.     KEYVNAME__4 = pack(keyname(REC_TYPE_NUMBER, EACH_KEY))
.     STRING_VARGET = trimlr(varget(KEYVNAME__4))
.     if(len(STR$2) gt 0) STR$2 = STR$2 +  ', '  | next set of values to pass through javascript
.     STR$2 = STR$2  + '''key' + format(EACH_KEY) + ''',''' + STRING_VARGET  + '''' | javascript passes strings with single qoutes
.     SHOW_KEYS__4 = SHOW_KEYS__4 + KEYVNAME__4 +  ':<b> ' + STRING_VARGET  + '</b>  '
.   end for
.     SHOW_KEYS__4 = SHOW_KEYS__4 + '<span class="xsmallfont"> at ' + fst(varget('DCCTIME'), '') +'</span>'

c   Keys (used for the link)
.   STR$2 = '"<mycgi>?member=WPF.CONTROLLER.REC_NAV&amp;caseis=' +  CASE_STR + '&amp;gdbrec=' + format(REC_TYPE_NUMBER) + '&amp;upick=View'
.   for EACH_KEY = 1 + dbtype(0), nkeys(REC_TYPE_NUMBER) +  dbtype(0)
.     KEYVNAME__4 = pack(keyname(REC_TYPE_NUMBER, EACH_KEY))
.     STRING_VARGET = trimlr(varget(KEYVNAME__4))
.     STR$2 =  STR$2 +  '&key' + format(EACH_KEY) + '='  + STRING_VARGET
.   end for
.   STR$2 = STR$2 + '"'
.   write(cgi) 4t '<a href=' STR$2 '>'
.   write(cgi) 4t SHOW_KEYS__4 '</a></li>'
end subprocedure


end retrieval
end repeat

program
write(CGI) '<h3>Total Records entered in: <GTOTAL_CNT></h3>'
end program

CIF END
END PROCEDURE
